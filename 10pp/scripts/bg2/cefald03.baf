IF
  Allegiance(Myself,ENEMY)
  Global("didOnce","LOCALS",0)
  HaveSpell(CLERIC_CURE_LIGHT_WOUNDS)
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    ApplySpell(Myself,CLERIC_RESIST_FIRE)
    ApplySpell(Myself,CLERIC_PROTECTION_FROM_LIGHTNING)
    ApplySpell(Myself,CLERIC_REGENERATE)
    SetGlobal("didOnce","LOCALS",1)
END

IF
  HaveSpell(CLERIC_IRONSKIN)
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    PickUpItem("staf04")
    FillSlot(SLOT_WEAPON1)
    Spell(Myself,CLERIC_IRONSKIN)
END

IF
  See(NearestEnemyOf(Myself))
  !Range(LastSeenBy(Myself),4)
  HPPercentLT(Myself,15)
  HaveSpell(CLERIC_HEAL)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_HEAL)
END

IF
  HaveSpell(CLERIC_SUMMON_INSECTS)
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),CLERIC_SUMMON_INSECTS)
END

IF
  See(NearestEnemyOf(Myself))
  GlobalLT("fighting","LOCALS",8)
THEN
  RESPONSE #100
    EquipMostDamagingMelee()
    AttackOneRound(LastSeenBy(Myself))
    IncrementGlobal("fighting","LOCALS",1)
END

IF
  HaveSpell(CLERIC_CURE_LIGHT_WOUNDS)
  HPLT(Myself,20)
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    ReallyForceSpell(Myself,FALDORN_PANTHER)
    RemoveSpell(CLERIC_CURE_LIGHT_WOUNDS)
END

IF
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    EquipMostDamagingMelee()
    Attack(LastSeenBy(Myself))
END


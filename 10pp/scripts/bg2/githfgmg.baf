IF
  See(NearestEnemyOf(Myself))
  Global("Sequencer","LOCALS",0)
THEN
  RESPONSE #100
    DisplayString(Myself,39969)  // ~Spell Sequencer - Fired~
    DisplayString(Myself,40697)  // ~Spell Sequencer - Haste~
    SetGlobal("Sequencer","LOCALS",1)
    ForceSpell(Myself,WIZARD_HASTE)
    ForceSpell(Myself,WIZARD_STONE_SKIN)
END

IF
  See(NearestEnemyOf(Myself))
  Global("Chaos","LOCALS",0)
THEN
  RESPONSE #100
    ForceSpell(NearestEnemyOf(Myself),WIZARD_CHAOS)
    SetGlobal("Chaos","LOCALS",1)
END

IF
  See(NearestEnemyOf(Myself))
  Global("MagicWeapon","LOCALS",0)
THEN
  RESPONSE #100
    ForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
    SetGlobal("MagicWeapon","LOCALS",1)
END

IF
  See(NearestEnemyOf(Myself))
  Global("Blast","LOCALS",0)
THEN
  RESPONSE #100
    ForceSpell(LastSeenBy(Myself),PSIONIC_MIND_BLAST)
    SetGlobal("Blast","LOCALS",1)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_POWER_WORD_SLEEP)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),WIZARD_POWER_WORD_SLEEP)
END

IF
  Global("gtMaze","LOCALS",0)
  OR(2)
    See(NearestEnemyOfType([0.0.0.FIGHTER_ALL]))
    See(NearestEnemyOfType([0.0.0.PALADIN_ALL]))
THEN
  RESPONSE #100
    ForceSpell(LastSeenBy(Myself),PSIONIC_MAZE)
    SetGlobal("gtMaze","LOCALS",1)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_HOLD_PERSON)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),WIZARD_HOLD_PERSON)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_FIRE_SHIELD_RED)
THEN
  RESPONSE #100
    Spell(Myself,WIZARD_FIRE_SHIELD_RED)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_POLYMORPH_OTHER)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),WIZARD_POLYMORPH_OTHER)
END

IF
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    AttackReevaluate(NearestEnemyOf(Myself),45)
END


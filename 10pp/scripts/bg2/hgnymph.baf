IF
  HaveSpell(CLERIC_BARKSKIN)
  See(NearestEnemyOf(Myself))
  Global("prep","LOCALS",0)
THEN
  RESPONSE #100
    ApplySpell(Myself,CLERIC_BARKSKIN)
    SetGlobal("prep","LOCALS",1)
END

IF
  See([GOODCUTOFF.MONSTER])
  Allegiance(Myself,ENEMY)
  HaveSpell(WIZARD_HOLD_MONSTER)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),WIZARD_HOLD_MONSTER)
END

IF
  See(NearestEnemyOf(Myself))
  RandomNum(3,1)
  !CheckStatGT(LastSeenBy(Myself),50,RESISTMAGIC)
  Global("charmonce","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("charmonce","LOCALS",1)
    SpellNoDec(NearestEnemyOf(Myself),NALMISSRA_CHARM)
    Continue()
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_MENTAL_DOMINATION)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),CLERIC_MENTAL_DOMINATION)
END

IF
  HaveSpell(CLERIC_HOLD_PERSON)
  See(NearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),CLERIC_HOLD_PERSON)
END

IF
  See(NearestEnemyOf(Myself))
  Global("castConfusion","LOCALS",0)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),NYMPH_CONFUSION)
    SetGlobal("castConfusion","LOCALS",1)
END

IF
  HaveSpell(CLERIC_CAUSE_SERIOUS_WOUNDS)
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),CLERIC_CAUSE_SERIOUS_WOUNDS)
    SetGlobal("lastSpell","LOCALS",1)
END

IF
  HaveSpell(CLERIC_MASS_CURE)
  HPPercentLT(Myself,80)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_MASS_CURE)
END

IF
  See(NearestEnemyOf(Myself))
  !Range(NearestEnemyOf(Myself),3)
  Global("lastSpell","LOCALS",1)
THEN
  RESPONSE #100
    ForceSpell(NearestEnemyOf(Myself),WIZARD_DIMENSION_DOOR)
END

IF
  HPPercentLT(Myself,25)
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    ReallyForceSpell(Myself,DRYAD_TELEPORT)
END

IF
  See(NearestEnemyOf(Myself))
  Global("lastSpell","LOCALS",1)
THEN
  RESPONSE #100
    AttackReevaluate(NearestEnemyOf(Myself),30)
END


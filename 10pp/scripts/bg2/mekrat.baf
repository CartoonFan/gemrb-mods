IF
  NumberOfTimesTalkedTo(0)
  See([PC])
THEN
  RESPONSE #100
    StartDialogNoSet([PC])
END

IF
  Global("MekrathAttacks","LOCALS",1)
  !Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    Enemy()
END

IF
  Global("MekrathImpatient","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("MekrathImpatient","LOCALS",2)
    SetGlobalTimer("MekrathStillHere","GLOBAL",10)
END

IF
  Global("MekrathImpatient","LOCALS",2)
  GlobalTimerExpired("MekrathStillHere","GLOBAL")
  Detect([PC])
  !Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    Enemy()
END

IF
  Global("MekrathImpatient","LOCALS",2)
  GlobalTimerExpired("MekrathStillHere","GLOBAL")
  !Detect([PC])
  !Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    SetGlobal("MekrathImpatient","LOCALS",0)
END

IF
  See(NearestEnemyOf(Myself))
  Global("PrepSpell0","LOCALS",0)
THEN
  RESPONSE #100
    ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_NORMAL_WEAPONS)
    ForceSpellPoint([139.489],WIZARD_ANIMATE_DEAD)
    SetGlobal("PrepSpell0","LOCALS",1)
END

IF
  See(NearestEnemyOf(Myself))
  Global("PrepSpell0","LOCALS",1)
THEN
  RESPONSE #100
    ForceSpellPoint([84.3154],WIZARD_ANIMATE_DEAD)
    ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_DEATH_SPELL)
    SetGlobal("PrepSpell0","LOCALS",2)
END


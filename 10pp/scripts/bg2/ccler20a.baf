IF
  See(NearestEnemyOf(Myself))
  See(NearestMyGroupOfType)
  Global("Preparation","LOCALS",0)
THEN
  RESPONSE #100
    ApplySpell(Myself,CLERIC_REGENERATE)
    ApplySpell(Myself,CLERIC_DEATH_WARD)
    ReallyForceSpell(LastSeenBy(Myself),CLERIC_DEFENSIVE_HARMONY)
    SetGlobal("Preparation","LOCALS",1)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(NPC_BLADE_BARRIER)
  RandomNum(2,1)
  Global("BladeBarrier","LOCALS",0)
THEN
  RESPONSE #100
    Spell(Myself,NPC_BLADE_BARRIER)
    SetGlobal("BladeBarrier","LOCALS",1)
END

IF
  See([GOODCUTOFF.0.SKELETON.0.0.SUMMONED])
  HaveSpell(CLERIC_SUNRAY)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_SUNRAY)
END

IF
  See(NearestEnemyOf(Myself))
  HitBy([ANYONE],CRUSHING)
  HaveSpell(CLERIC_IRONSKIN)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_IRONSKIN)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_PROTECTION_FROM_EVIL_10_FOOT)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_PROTECTION_FROM_EVIL_10_FOOT)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_FREE_ACTION)
  See(NearestMyGroupOfType)
  StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),CLERIC_FREE_ACTION)
END

IF
  See(NearestEnemyOf(Myself))
  See(NearestMyGroupOfType)
  HaveSpell(CLERIC_MASS_CURE)
  HPPercentLT(Myself,80)
  OR(3)
    HPPercentLT(LastSeenBy(Myself),80)
    HPPercentLT(SecondNearestMyGroupOfType,80)
    HPPercentLT(ThirdNearestMyGroupOfType,80)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),CLERIC_MASS_CURE)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_STRENGTH_OF_ONE)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_STRENGTH_OF_ONE)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_CHANT)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_CHANT)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_RIGHTEOUS_MAGIC)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_RIGHTEOUS_MAGIC)
END

IF
  See(NearestEnemyOf(Myself))
  See(NearestMyGroupOfType)
  HPPercentLT(LastSeenBy(Myself),10)
  HaveSpell(CLERIC_HEAL)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),CLERIC_HEAL)
END

IF
  See(NearestEnemyOf(Myself))
  !Range(LastSeenBy(Myself),4)
  HPPercentLT(Myself,10)
  HaveSpell(CLERIC_HEAL)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_HEAL)
END

IF
  Heard([ANYONE],153)
  HaveSpell(CLERIC_HEAL)
THEN
  RESPONSE #100
    Spell(LastHeardBy(Myself),CLERIC_HEAL)
END

IF
  Heard([ANYONE],153)
  HaveSpell(CLERIC_CURE_CRITICAL_WOUNDS)
THEN
  RESPONSE #100
    Spell(LastHeardBy(Myself),CLERIC_CURE_CRITICAL_WOUNDS)
END

IF
  Heard([ANYONE],153)
  HaveSpell(CLERIC_CURE_SERIOUS_WOUNDS)
THEN
  RESPONSE #100
    Spell(LastHeardBy(Myself),CLERIC_CURE_SERIOUS_WOUNDS)
END

IF
  Heard([ANYONE],153)
  HaveSpell(CLERIC_CURE_MEDIUM_WOUNDS)
THEN
  RESPONSE #100
    Spell(LastHeardBy(Myself),CLERIC_CURE_MEDIUM_WOUNDS)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_CONJURE_FIRE_ELEMENTAL)
  RandomNum(2,1)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),CLERIC_CONJURE_FIRE_ELEMENTAL)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_CONJURE_EARTH_ELEMENTAL)
  RandomNum(2,2)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),CLERIC_CONJURE_EARTH_ELEMENTAL)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_AERIAL_SERVANT)
  RandomNum(2,1)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),CLERIC_AERIAL_SERVANT)
END

IF
  HaveSpell(CLERIC_SILENCE_15_FOOT)
  OR(2)
    See(NearestEnemyOfType([0.0.0.MAGE_ALL]))
    See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),CLERIC_SILENCE_15_FOOT)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_CHARM_PERSON)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),CLERIC_CHARM_PERSON)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_HOLD_PERSON)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),CLERIC_HOLD_PERSON)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_CLOAK_OF_FEAR)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_CLOAK_OF_FEAR)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_PHYSICAL_MIRROR)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_PHYSICAL_MIRROR)
END

IF
  See(NearestEnemyOf(Myself))
  !Range(NearestEnemyOf(Myself),7)
THEN
  RESPONSE #100
    EquipRanged()
    AttackReevaluate(NearestEnemyOf(Myself),45)
END

IF
  See(NearestEnemyOf(Myself))
  Range(NearestEnemyOf(Myself),7)
THEN
  RESPONSE #100
    EquipMostDamagingMelee()
    AttackReevaluate(NearestEnemyOf(Myself),45)
END


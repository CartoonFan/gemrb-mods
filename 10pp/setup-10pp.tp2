BACKUP ~10pp/backup~
AUTHOR ~lynx, CrevsDaak~
VERSION "10 player parties 0.9"

BEGIN ~Maximum party size extender for IEScript (usable with GemRB only)~

ACTION_IF (ENGINE_IS ~bg2 tob~ AND (NOT MOD_IS_INSTALLED ~setup-bg2fixpack.tp2~ ~0~))
THEN BEGIN
  FAIL ~For bg2, the G3 Fixpack is needed (Core fixes): www.gibberlings3.net/bg2fixpack/~
END

// some warnings and input
PRINT ~
This mod should be installed after all other script altering/adding mods!

The conversion process can easily take several minutes depending on the size of your install!
Please be patient and DO NOT INTERRUPT execution lest you want to start from zero.
There is only one query and then you can go plant some coffee.

What should the new maximum party size be? [1-10]~
ACTION_READLN ~newmax~
OUTER_WHILE NOT(IS_AN_INT %newmax% && %newmax% > 0 && %newmax% <= 10) BEGIN
  PRINT ~Enter a whole number between 1 and 10 silly.
What should the new maximum party size be? [1-10]~
  ACTION_READLN ~newmax~
END

// if it is <6 then they only have to change the variable in GemRB config
ACTION_IF (%newmax% < 7) THEN BEGIN
  PRINT ~Good news, smaller parties require only configuration changes!

Just set MaxPartySize to %newmax% in your GemRB config file and you'll be ready to play.
~
  FAIL ~Nothing to change, exiting...~
END

// extend object.ids with new scripting objects
// NOTE: gemrb supports only up to 10 objects (an arbitrary limit until someone shows a need for more)
APPEND ~object.ids~ ~90 Player7
91 Player8
92 Player9
93 Player10
94 Player7Fill
95 Player8Fill
96 Player9Fill
97 Player10Fill~

// decompile all to-be affected scripts
MKDIR ~10pp/temp~
MKDIR ~10pp/temp/baf~
MKDIR ~10pp/temp/d~

ACTION_BASH_FOR ~10pp/temp/baf~ ~^.*$~ BEGIN
  DELETE + ~%BASH_FOR_FILESPEC%~
END
ACTION_BASH_FOR ~10pp/temp/d~ ~^.*$~ BEGIN
  DELETE + ~%BASH_FOR_FILESPEC%~
END

PRINT ~Fixing scripts...~

COPY_EXISTING_REGEXP + GLOB ~^.*\.bcs$~ ~10pp/temp/baf~
  COUNT_REGEXP_INSTANCES ~ 26 .*"OB~ has_player_6
  PATCH_IF has_player_6 BEGIN
    DECOMPILE_BCS_TO_BAF
  END
BUT_ONLY

OUTER_SET RC = 0
ACTION_BASH_FOR ~10pp/temp/baf~ ~^.*\.bcs$~ BEGIN
  AT_NOW RC ~perl 10pp/wrapper.pl "%BASH_FOR_FILESPEC%" %newmax% "%BASH_FOR_DIRECTORY%/%BASH_FOR_RES%.baf"~
  ACTION_IF (%RC% > 0) THEN BEGIN
    FAIL ~Oops, better check that script!~
  END
END

COMPILE ~10pp/temp/baf~ // this will create backup files... hope so at least...

// fix all dialogs
PRINT ~Fixing dialogs...~
COPY_EXISTING_REGEXP + GLOB ~^.*\.dlg$~ ~10pp/temp/d~
  COUNT_REGEXP_INSTANCES ~Player6~ has_player_6
  PATCH_IF has_player_6 BEGIN
    DECOMPILE_DLG_TO_D
  END
BUT_ONLY

ACTION_BASH_FOR ~10pp/temp/d~ ~^.*\.dlg$~ BEGIN
  AT_NOW RC ~perl 10pp/wrapper.pl "%BASH_FOR_FILESPEC%" %newmax% "%BASH_FOR_DIRECTORY%/%BASH_FOR_RES%.d"~
  ACTION_IF (%RC% > 0) THEN BEGIN
    FAIL ~Oops, better check that dialog!~
  END
END

COMPILE ~10pp/temp/d~ // this will create backup files... hope so at least...

// final instructions
PRINT ~DONE!

REMEMBER to:
* use a big enough resolution (extra space for portraits)
* and to set MaxPartySize to %newmax% in your GemRB config file
* more details are avaiable in the README: https://github.com/lynxlynxlynx/gemrb-mods/tree/master/10pp
~

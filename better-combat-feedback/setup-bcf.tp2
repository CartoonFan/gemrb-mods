BACKUP ~better-combat-feedback/backup~
AUTHOR ~lynxlynxlynx~

VERSION ~1.5~
MODDER

BEGIN ~IWD2/HOW style detailed combat feedback for other games in GemRB~

ACTION_IF (GAME_IS ~how totlm iwd2~) THEN BEGIN
  FAIL ~ABORTING: installing this mod on HoW or IWD2 makes no sense!~
END

// prepare the new feedback text
// eh, weidu can only handle strings in this construct
ACTION_DEFINE_ARRAY feedback BEGIN
  ~<DAMAGEE> was immune to my <TYPE> damage~
  ~Takes <AMOUNT> <TYPE> damage from <DAMAGER>~
  ~Takes <AMOUNT> <TYPE> damage from <DAMAGER> (<RESISTED> damage resisted)~
  ~Takes <AMOUNT> <TYPE> damage from <DAMAGER> (<RESISTED> damage bonus)~
  ~poison~
  ~magic~
  ~missile~
  ~slashing~
  ~piercing~
  ~crushing~
  ~fire~
  ~electric~
  ~cold~
  ~acid~
END

// save the strings.2da row names
ACTION_DEFINE_ARRAY strmarks BEGIN
  ~DAMAGE_IMMUNITY~ ~DAMAGE_STR1~ ~DAMAGE_STR2~ ~DAMAGE_STR3~
  ~DMG_POISON~ ~DMG_MAGIC~ ~DMG_MISSILE~
  ~DMG_SLASHING~ ~DMG_PIERCING~ ~DMG_CRUSHING~
  ~DMG_FIRE~ ~DMG_ELECTRIC~ ~DMG_COLD~ ~DMG_ACID~
END

// modify strings.2da and copy it over
COPY_EXISTING ~strings.2da~ ~override~
  FOR (i=0; i<14; i+=1) BEGIN
	SPRINT ref $feedback("%i%")
	SET ref = RESOLVE_STR_REF("%ref%")
	SPRINT mark $strmarks("%i%")
    REPLACE_TEXTUALLY ~%mark% .*~ ~%mark% %ref%~
  END
